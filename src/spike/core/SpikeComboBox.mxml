<?xml version="1.0" encoding="utf-8"?>
<mx:ComboBox xmlns:mx="http://www.adobe.com/2006/mxml"
    rowCount="10">

  <mx:Script>
    <![CDATA[
      import mx.events.ListEvent;

      private var selectedValue_:String;
      private var selectedValueSet_:Boolean = false;
      private var dataProviderSet_:Boolean = false;

      override protected function commitProperties():void {
        super.commitProperties();
        if (selectedValueSet_ && dataProviderSet_) {
          selectedValueSet_ = false;
          for (var i:int = 0; i < this.dataProvider.length; i++) {
            var item:String = this.dataProvider[i].data;
            if(item == selectedValue_) {
              this.selectedIndex = i;
              break;
            }
          }
        }
      }

      override public function set dataProvider(o:Object):void {
        super.dataProvider = o;
        if (o != null && o.length)
          dataProviderSet_ = true;
      }

      public function set selectedValue(s:String):void {
        selectedValueSet_ = true;
        selectedValue_ = s;
        invalidateProperties();
      }

      public function get selectedValue():String {
        return this.dataProvider[this.selectedIndex].data;
      }
    ]]>
  </mx:Script>
</mx:ComboBox>
