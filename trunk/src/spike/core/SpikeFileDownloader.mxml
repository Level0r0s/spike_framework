<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
    creationComplete="onCreationComplete(event)"
    width="100%">

  <mx:Script>
    <![CDATA[
      import org.restfulx.services.http.XMLHTTPServiceProvider;
      import org.restfulx.serializers.GenericSerializer;
      import org.restfulx.controllers.SerializersController;
      import mx.controls.Alert;
      import mx.core.Application;
      import mx.core.IFlexDisplayObject;
      import mx.events.FlexEvent;
      import mx.managers.PopUpManager;

      [Bindable]
      public var url:String;

      [Bindable]
      public var fileName:String;

      [Bindable]
      public var bypassToBrowser:Boolean = true;

      [Bindable]
      protected var fileReference:FileReference;

      [Bindable]
      protected var showCancel:Boolean = false;
      [Bindable]
      protected var showClose:Boolean = false;

      protected var popUpWindow:DisplayObject;
      protected var popUp:IFlexDisplayObject;
      
      [Bindable]
      public var frameName:String = "_blank";

      protected function onCreationComplete(event:FlexEvent):void {
        popUpWindow = progressWindow;
        popUp = popUpWindow as IFlexDisplayObject;
        removeChild(popUpWindow);
      }

      public function download():void {
        if (url != '') {
          var request:URLRequest = new URLRequest(url);
          request.data = data;
          request.method = URLRequestMethod.GET;
          if (bypassToBrowser) {
            //navigateToURL(request, frameName); // da pau no chrome
            if (data) {
              url += '?'+data;
            }
            ExternalInterface.call('downloadURL', url);
          } else {
            fileReference = new FileReference();

            fileReference.addEventListener(Event.OPEN, onOpen);
            fileReference.addEventListener(ProgressEvent.PROGRESS, onProgress);
            fileReference.addEventListener(Event.CANCEL, onCancel);
            fileReference.addEventListener(Event.COMPLETE, onComplete);
            fileReference.addEventListener(IOErrorEvent.IO_ERROR, onIoError);
            fileReference.addEventListener(SecurityErrorEvent.SECURITY_ERROR, onSecurityError);

            showPopUp();
            fileReference.download(request, fileName);
          }
        }
      }

      protected function onOpen(event:Event):void {
      }

      protected function onProgress(event:ProgressEvent):void {
        progress.label = "%1 de %2 bytes obtidos...";
        progress.indeterminate = false;
      }

      protected function onCancel(event:Event):void {
        progress.label = "Download cancelado!";
        hidePopUp();
      }

      protected function onComplete(event:Event):void {
        progress.label = "Download concluÃ­do com sucesso!";
        showCancel = false;
        showClose = true;
      }

      protected function onIoError(event:IOErrorEvent):void {
        hidePopUp();
        Alert.show(event.text, 'Erro ao fazer o download do arquivo!');
      }

      protected function onSecurityError(event:SecurityErrorEvent):void {
        hidePopUp();
        Alert.show(event.text, 'Erro ao fazer o download do arquivo!');
      }

      private function showPopUp():void {
        progress.label = "Iniciando download...";
        showCancel = true;
        showClose = false;
        PopUpManager.addPopUp(popUp, this, false);
      }

      private function hidePopUp():void {
        PopUpManager.removePopUp(popUp);
      }

      protected function cancel():void {
        fileReference.cancel();
        hidePopUp();
      }

      protected function close():void {
        hidePopUp();
      }
    ]]>
  </mx:Script>

  <mx:TitleWindow id="progressWindow" title="Download de arquivo"
      backgroundColor="#EEEEEE" layout="vertical" width="300"
      x="{(Application.application.width / 2) - (progressWindow.width / 2)}"
      y="{(Application.application.height / 2) - (progressWindow.height / 2)}"
      showCloseButton="{showClose}" close="close()"
      paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
    <mx:ProgressBar id="progress" source="{fileReference}" width="100%" />
    <mx:HBox width="100%">
      <mx:Button id="cancelButton" label="Cancelar" click="cancel()"
          visible="{showCancel}" includeInLayout="{showCancel}" />
      <mx:Button id="closeButton" label="Fechar" click="close()"
          visible="{showClose}" includeInLayout="{showClose}" />
    </mx:HBox>
  </mx:TitleWindow>
</mx:Canvas>
